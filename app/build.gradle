import com.config.Switch
import com.config.Versions
import com.config.Accompanist

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def APP_NAME
def PKGNAME
def versionType

android {

    signingConfigs {
        config {
            keyAlias 'yoojin'
            keyPassword '123456'
            storeFile file('../jks/yoojin.jks')
            storePassword '123456'
        }
    }

    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools

    defaultConfig {
        if (Switch.workType == 0){
            PKGNAME = "com.strong"
            versionType = "release"
            buildConfigField 'String', 'baseUrl', "\"https://files.cnblogs.com/files/LiuZhen/\""
        }else{
            PKGNAME = "com.strong.test"
            versionType = "test"
            buildConfigField 'String', 'baseUrl', "\"http://10.10.13.214:81//\""
        }
        APP_NAME = "KtxApp-"+versionType
        applicationId PKGNAME
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName

        resConfigs('zh-rCN')//避免生成不必要的资源文件（默认是英语，不能剔除）

        manifestPlaceholders = [
                PKGNAME: PKGNAME,// 包名
                APP_NAME    : APP_NAME,//  app名称
        ]
        ndk {//externalNativeBuildDebug
            /** CPU类型
             * armeabiv-v7a: 第7代及以上的ARM处理器;
             * arm64-v8a: 第8代、64位ARM处理器;
             * armeabi: 第5代、第6代的ARM处理器;（平台不支持）
             * mips:一种RISC处理器。（平台不支持）
             * mips64:64位的。（平台不支持）
             * x86: 平板;
             * x86_64:64位的平板
             * */
            if (Switch.isX86) {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            } else {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
        //矢量图
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            //混淆
            minifyEnabled false
            //移除无用的resource文件
            shrinkResources false
            //Zipalign优化
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    composeOptions {
        kotlinCompilerExtensionVersion Versions.compose
        kotlinCompilerVersion Versions.kotlin
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose = true
    }
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${APP_NAME}-${defaultConfig.versionName}-${rootProject.releaseTime()}-${versionType}.apk"
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //kotlin
    implementation "androidx.core:core-ktx:${Versions.core_ktx}"
    //google
    implementation "com.google.android.material:material:${Versions.material}"
    implementation "androidx.compose.ui:ui:${Versions.compose}"
    implementation "androidx.compose.material:material:${Versions.compose}"
    implementation "androidx.compose.ui:ui-tooling:${Versions.compose}"
    implementation "androidx.activity:activity-compose:${Versions.composeActivity}"
    //mmkv
    implementation "com.tencent:mmkv-static:${Versions.mmkv}"
    //utils
    implementation "com.blankj:utilcodex:${Versions.blankjUtils}"
    //gson
    implementation "com.google.code.gson:gson:${Versions.gson}"
    //glide
    implementation "com.github.bumptech.glide:glide:${Versions.glide}"
    implementation "jp.wasabeef:glide-transformations:${Versions.transformations}"
    kapt "com.github.bumptech.glide:compiler:${Versions.glide}"
    //accompanist
    implementation Accompanist.insets
}